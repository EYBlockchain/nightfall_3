from "../common/utils/structures.zok" import Point, PublicTransaction, Nullifiers, Commitments, Transfer, SHIFT
from "../common/utils/calculations.zok" import sum
from "../common/casts/u32_array_to_field.zok" import main as u32_array_to_field
from "../common/generic_circuit/Verifiers/verify_structure.zok" import main as verify_structure
from "../common/generic_circuit/Verifiers/verify_encryption.zok" import main as verify_encryption
from "../common/generic_circuit/Verifiers/verify_nullifiers.zok" import main as verify_nullifiers
from "../common/generic_circuit/Verifiers/verify_commitments.zok" import main as verify_commitments

def main(\
    PublicTransaction tx,\
    field[2] nullifierRoots,\
    field ercAddressFee,\
    u32[8] tokenIdFee,\
    field[2] nullifierRootsFee,\
    private Nullifiers<2> nullifiers,\
    private Commitments<2> commitments,\
    private Nullifiers<2> nullifiersFee,\
    private Commitments<1> commitmentsFee\
)-> ():
    
    //Verify public transaction structure
    assert(verify_structure::<2>(\
        tx.value,\
	    tx.transactionType,\
	    tx.tokenType,\
	    tx.tokenId,\
	    tx.ercAddress,\
	    tx.recipientAddress,\
	    tx.commitments,\
	    tx.nullifiers,\
	    tx.historicRootBlockNumberL2,\
	    tx.compressedSecrets,\
        tx.fee,\
        tx.commitmentsFee,\
        tx.nullifiersFee\
    ))

    //Check that values match
    assert(sum(nullifiers.oldCommitments.value) == sum(commitments.newCommitments.value) + tx.value)

    //Check that values fee match
    assert(sum(nullifiersFee.oldCommitments.value) == tx.fee + sum(commitmentsFee.newCommitments.value))

    // pack the top four bytes of the token id into the ercAddress field (address only
	// uses 160 bits and the Shield contract prevents creation of something with more than 160 bits)
    field idRemainder = u32_array_to_field(tx.tokenId[1..8]) 
    field packedErcAddress = tx.ercAddress + u32_array_to_field([tx.tokenId[0]]) * SHIFT 

   
    Point firstInputZkpPublicKeys = verify_nullifiers::<2>(packedErcAddress, idRemainder,\
        tx.nullifiers, nullifierRoots, nullifiers.oldCommitments.value, nullifiers.oldCommitments.salt,\
        nullifiers.rootKey, nullifiers.paths, nullifiers.orders)

    //Verify new Commmitments
    assert(verify_commitments::<2, 2>(packedErcAddress, idRemainder, tx.commitments, firstInputZkpPublicKeys,\
    commitments.newCommitments.value, commitments.newCommitments.salt, commitments.recipientPublicKey))

    //Verify Change
    //TODO: assert(newCommitmentValues[NumCommitments - 1] == 0 || changeZkpPublicKeys == recipientPublicKey[NumCommitments - 1])

    field idRemainderFee = u32_array_to_field(tokenIdFee[1..8]) 
    field packedErcAddressFee = ercAddressFee + u32_array_to_field([tokenIdFee[0]]) * SHIFT

    //Verify Fee Nullifiers
    Point firstInputZkpPublicKeysFee = verify_nullifiers::<2>(packedErcAddressFee, idRemainderFee,\
        tx.nullifiersFee, nullifierRootsFee, nullifiersFee.oldCommitments.value, nullifiersFee.oldCommitments.salt,\
        nullifiersFee.rootKey, nullifiersFee.paths, nullifiersFee.orders)

    //Verify Fee Commitments
    assert(verify_commitments::<1, 2>(packedErcAddressFee, idRemainderFee, firstInputZkpPublicKeysFee,\
    tx.commitmentsFee, commitmentsFee.newCommitments.value,commitmentsFee.newCommitments.salt,\
    commitmentsFee.recipientPublicKey))

    return
