from "hashes/poseidon/poseidon.zok" import main as poseidon
from "../../common/utils/structures.zok" import Point, NUMBER_COMMITMENTS

def main<NumCommitments, TxType>(\
    field packedErcAddress,\
    field idRemainder,\
    field[NUMBER_COMMITMENTS] commitments,\
    field[2] changeZkpPublicKeys,\
    private field[NumCommitments] newCommitmentValues,\
    private field[NumCommitments] newCommitmentSalts,\
    private Point[NumCommitments] recipientPublicKey\
) -> bool:

    bool change = if (TxType == 2 && NumCommitments > 0) || (TxType == 1 && NumCommitments > 1) then true else false fi 

    for u32 i in 0..NumCommitments do
        field outputCommitment = poseidon([\
            packedErcAddress,\
            idRemainder,\
            newCommitmentValues[i],\
            ...recipientPublicKey[i],\
            newCommitmentSalts[i]\
        ])
      
        assert(outputCommitment == commitments[i])
        assert(change == false || i != NumCommitments - 1 || changeZkpPublicKeys == recipientPublicKey[i])
    endfor
    
    
    return true