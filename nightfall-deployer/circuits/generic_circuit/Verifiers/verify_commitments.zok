from "hashes/poseidon/poseidon.zok" import main as poseidon
from "../../common/utils/structures.zok" import Point, NUMBER_COMMITMENTS

def main<COuts, TxType>(\
    field packedErcAddress,\
    field idRemainder,\
    field[NUMBER_COMMITMENTS] commitments,\
    field[2] changeZkpPublicKeys,\
    private field[COuts] newCommitmentValues,\
    private field[COuts] newCommitmentSalts,\
    private Point[COuts] recipientPublicKey\
) -> bool:

    bool change = if (TxType == 2 && COuts > 0) || (TxType == 1 && COuts > 1) then true else false fi 

    for u32 i in 0..COuts do
        field outputCommitment = poseidon([\
            packedErcAddress,\
            idRemainder,\
            newCommitmentValues[i],\
            ...recipientPublicKey[i],\
            newCommitmentSalts[i]\
        ])
      
        assert(outputCommitment == commitments[i])
        assert(change == false || i != COuts - 1 || changeZkpPublicKeys == recipientPublicKey[i])
    endfor
    
    
    return true