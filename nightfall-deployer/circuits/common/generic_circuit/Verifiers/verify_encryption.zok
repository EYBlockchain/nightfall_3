from "ecc/edwardsCompress" import main as edwardsCompress;
from "utils/pack/bool/nonStrictUnpack256.zok" import main as field_to_bool_256;
from "utils/casts/u32_8_to_bool_256.zok" import main as u32_8_to_bool_256;
from "../../casts/u32_array_to_field.zok" import main as u32_array_to_field;
from "../../encryption/kem-dem.zok" import main as kemDem, EncryptedMsgs;
from "../../utils/structures.zok" import Point;

/*
* Verify that the secrets are encrypted properly in the Transfer
*/
def main(\
    field ercAddress,\
    u32[8] tokenId,\
    field[2] compressedSecrets,\
    field packedErcAddress,\
    field idRemainder,\
    private field newCommitmentValues,\
    private field newCommitmentSalts,\
    private Point recipientPublicKey,\
    u32[8] recipientAddress,\
    private u32[8] ephemeralKey\
) -> bool {

    field[4] cipherText = [ercAddress,u32_array_to_field(tokenId),compressedSecrets[0],compressedSecrets[1]];
    bool[256] bitEphemeralKey = u32_8_to_bool_256(ephemeralKey);

    field[4] plainTexts = [\
        packedErcAddress,\
        idRemainder,\
        newCommitmentValues,\
        newCommitmentSalts\
    ];

    EncryptedMsgs<4> enc = kemDem(bitEphemeralKey, [recipientPublicKey[0], recipientPublicKey[1]], plainTexts);
    assert(cipherText == enc.cipherText);

    bool[256] compressedPubKeyOutput = edwardsCompress(enc.ephemeralPublicKey);
    assert(compressedPubKeyOutput == u32_8_to_bool_256(recipientAddress));
    return true;
}
