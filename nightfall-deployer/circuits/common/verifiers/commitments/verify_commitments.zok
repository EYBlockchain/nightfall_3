from "hashes/poseidon/poseidon.zok" import main as poseidon;
from "../../utils/structures.zok" import Point;

/*
* Verify that all commitments are correct and corresponds to packedErcAddress.
*/
def main<C>(\
    field packedErcAddress,\
    field idRemainder,\
    field[C] commitmentHashes,\
    private field[C] newCommitmentValues,\
    private field[C] newCommitmentSalts,\
    private Point[C] recipientPublicKey\
) -> bool {

    for u32 i in 0..C {
        // Calculate commmitment hash
        field commitment = poseidon([\
                packedErcAddress,\
                idRemainder,\
                newCommitmentValues[i],\
                ...recipientPublicKey[i],\
                newCommitmentSalts[i]\
            ]);
    
        //Check that the calculated commitment matches with the one contained in the transaction
        assert(commitment == commitmentHashes[i]);
    }    
    
    return true;
}