
from "./common/utils/structures.zok" import Point, PublicTransaction, Nullifiers, Commitments, Transfer, SHIFT
from "./common/utils/calculations.zok" import sum
from "./common/casts/u32_array_to_field.zok" import main as u32_array_to_field
from "./common/casts/u8_array_to_field.zok" import main as u8_array_to_field
from "./common/generic_circuit/Verifiers/verify_structure.zok" import main as verify_structure
from "./common/generic_circuit/Verifiers/verify_encryption.zok" import main as verify_encryption
from "./common/generic_circuit/Verifiers/verify_nullifiers.zok" import main as verify_nullifiers
from "./common/generic_circuit/Verifiers/verify_commitments.zok" import main as verify_commitments

def main(\
    PublicTransaction tx,\
    field[2] roots,\
    field[2] rootsFee,\
    field ercAddressFee,\
    private Nullifiers<2> nullifiers,\
    private Commitments<2> commitments,\
    private Nullifiers<2> nullifiersFee,\
    private Commitments<1> commitmentFee,\
    private Transfer transfer\
)-> ():
    

    //Verify public transaction structure
    assert(verify_structure::<1>(\
        tx.value,\
        tx.fee,\
	    tx.transactionType,\
	    tx.tokenType,\
	    tx.tokenId,\
	    tx.ercAddress,\
	    tx.recipientAddress,\
	    tx.commitments,\
	    tx.nullifiers,\
        tx.commitmentFee,\
        tx.nullifiersFee,\
	    tx.historicRootBlockNumberL2,\
        tx.historicRootBlockNumberL2Fee,\
	    tx.compressedSecrets\
    ))

    field[2] nullifiersValue = u8_array_to_field(nullifiers.oldCommitments.value)
    field[2] commitmentsValue = u8_array_to_field(commitments.newCommitments.value)

    field[2] nullifiersFeeValue = u8_array_to_field(nullifiersFee.oldCommitments.value)
    field[1] commitmentFeeValue = u8_array_to_field(commitmentFee.newCommitments.value)


    //Check that values match
    assert(sum(nullifiersValue) == sum(commitmentsValue))

    //Check that values fee match
    assert(sum(nullifiersFeeValue) == tx.fee + sum(commitmentFeeValue))

    // pack the top four bytes of the token id into the ercAddress field (address only
	// uses 160 bits and the Shield contract prevents creation of something with more than 160 bits)
    field idRemainder = u32_array_to_field(transfer.idTransfer[1..8]) 
    field packedErcAddress = transfer.ercAddressTransfer + u32_array_to_field([transfer.idTransfer[0]]) * SHIFT 

    //Verify nullifiers
    Point firstInputZkpPublicKeys = verify_nullifiers::<1,2>(packedErcAddress, idRemainder,\
        tx.nullifiers, roots, nullifiersValue, nullifiers.oldCommitments.salt,\
        nullifiers.rootKey, nullifiers.paths, nullifiers.orders)

    //Verify new Commmitments
    assert(verify_commitments::<1,2>(packedErcAddress, idRemainder, tx.commitments,\
    commitmentsValue, commitments.newCommitments.salt, commitments.recipientPublicKey))
        
    //Verify Change
    assert(commitmentsValue[1] == 0 || firstInputZkpPublicKeys == commitments.recipientPublicKey[1])
    
    //Verify Kem Dem encryption
    assert(verify_encryption(tx.ercAddress,tx.tokenId, tx.compressedSecrets,\
        packedErcAddress,idRemainder,commitmentsValue[0],commitments.newCommitments.salt[0],\
        commitments.recipientPublicKey[0],tx.recipientAddress, transfer.ephemeralKey))
    
    //Verify nullifiers fee
    Point firstInputZkpPublicKeysFee = verify_nullifiers::<0,2>(ercAddressFee, 0,\
        tx.nullifiersFee, rootsFee, nullifiersFeeValue, nullifiersFee.oldCommitments.salt,\
        nullifiersFee.rootKey, nullifiersFee.paths, nullifiersFee.orders)

    //Verify new Commmitments
    assert(verify_commitments::<0,1>(ercAddressFee, 0, tx.commitmentFee,\
    commitmentFeeValue, commitmentFee.newCommitments.salt, commitmentFee.recipientPublicKey))
        
    //Verify Fee change
    assert(commitmentFeeValue[0] == 0 || firstInputZkpPublicKeysFee == commitmentFee.recipientPublicKey[0])

    return
