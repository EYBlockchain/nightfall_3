from "ecc/babyjubjubParams" import BabyJubJubParams
from "ecc/babyjubjubParams" import main as curveParams
from "ecc/edwardsCompress" import main as edwardsCompress
from "ecc/edwardsScalarMult" import main as scalarMult

from "utils/pack/bool/nonStrictUnpack256.zok" import main as field_to_bool_256
from "./common/casts/u8_array_to_field.zok" import main as u8_array_to_field
from "./common/casts/u32_array_to_field.zok" import main as u32_array_to_field

from "hashes/poseidon/poseidon.zok" import main as poseidon
from "./common/merkle-tree/path-check.zok" import main as pathCheck

// 2 ^ 160
const field SHIFT = 1461501637330902918203684832716283019655932542976

struct OldCommitmentPreimage {
	field salt
	field hash
	field rootKey
}

def main(\
	field ercAddress,\
	u32[8] id,\
 	field value,\
	private OldCommitmentPreimage oldCommitment,\
	field nullifier,\
	field recipientAddress,\
	field root,\
	private field[32] path,\
	private field order\
)->():

	BabyJubJubParams context = curveParams()
	field[2] g = [context.Gu, context.Gv]

	// Calculation of zkpPrivateKey and nullifierKey from rootKey
	field zkpPrivateKey = poseidon([oldCommitment.rootKey, 2708019456231621178814538244712057499818649907582893776052749473028258908910])
	field nullifierKey = poseidon([oldCommitment.rootKey, 7805187439118198468809896822299973897593108379494079213870562208229492109015])
	bool[256] zkpPrivateKeyBool = field_to_bool_256(zkpPrivateKey)
	field[2] zkpPublicKey = scalarMult(zkpPrivateKeyBool, g, context)

	// check the nullifier is valid
	field nullifierCheck = poseidon([\
		nullifierKey,\
		oldCommitment.hash\
	])
	assert(nullifierCheck == nullifier)

	// pack the top four bytes of the token id into the ercAddress field (address only
	// uses 160 bits and the Shield contract prevents creation of something with more than 160 bits)
	field idTop4Bytes = u32_array_to_field([id[0]])
	field idRemainder = u32_array_to_field(id[1..8])
	field packedErcAddress = ercAddress + idTop4Bytes * SHIFT

	// check the old commitment is valid
	field oldCommitmentCheck = poseidon([\
		packedErcAddress,\
		idRemainder,\
		value,\
		...zkpPublicKey,\
		oldCommitment.salt\
	])
	assert(oldCommitmentCheck == oldCommitment.hash)

	// check that the old commitment is in the merkle tree
	field hash = oldCommitment.hash
	assert(pathCheck([root, ...path], order, hash))

	return
